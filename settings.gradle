rootProject.name = 'pruebalegoback'

def excludes = gradle.startParameter.projectProperties.get("excludeProjects")?.split(",")

def buildFiles = fileTree(rootDir) {
    include '**/*.gradle'
    exclude 'main.gradle', 'build', '**/gradle', 'settings.gradle', 'buildSrc', '/build.gradle', '.*', 'out'
    exclude '**/infraestructure/driven-adapters/r2dbc/**'  
    if (excludes) {
        exclude excludes
    }
}

String rootDirPath = rootDir.absolutePath + File.separator
buildFiles.each { File buildFile ->

    boolean isDefaultName = 'build.gradle'.equals(buildFile.name)
    if (isDefaultName) {
        String projectName = buildFile.parentFile.name
        String projectPath = ':' + projectName
        include projectPath
        def project = findProject(projectPath)
        project.name = buildFile.parentFile.parentFile.name + '-' + projectName
        project.projectDir = buildFile.parentFile
        project.buildFileName = buildFile.name

    } else {
        String projectName = buildFile.name.replace('.gradle', '')
        String projectPath = ':' + projectName
        include projectPath
        def project = findProject(projectPath)
        project.name = projectName
        project.projectDir = buildFile.parentFile
        project.buildFileName = buildFile.name
    }
}

include ':infraestructure:driven-adapters:r2dbc'
include ':entry-points-subs-events'